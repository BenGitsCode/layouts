<html><head><title>Layouts</title><link href="http://fonts.googleapis.com/css?family=Voces" rel="stylesheet" type="text/css"/><link rel="stylesheet" href="dist/common-343022c5.css"/><script>var _gaq = _gaq || [];
_gaq.push(['_setAccount', 'UA-24017782-2']);
_gaq.push(['_trackPageview']);
(function() {
  var ga = document.createElement('script'); ga.type = 'text/javascript'; ga.async = true;
  ga.src = ('https:' == document.location.protocol ? 'https://ssl' : 'http://www') + '.google-analytics.com/ga.js';
  var s = document.getElementsByTagName('script')[0]; s.parentNode.insertBefore(ga, s);
})();
</script></head><body><div id="toc"></div><a href="http://github.com/jgallen23/layouts"><img style="position: absolute; top: 0; right: 0; border: 0;" src="https://a248.e.akamai.net/assets.github.com/img/7afbc8b248c68eb468279e8c17986ad46549fb71/687474703a2f2f73332e616d617a6f6e6177732e636f6d2f6769746875622f726962626f6e732f666f726b6d655f72696768745f6461726b626c75655f3132313632312e706e67" alt="Fork me on GitHub"/></a><div id="wrapper"><h1>Layouts</h1>
<p>Layouts is an applescript and an <a href="http://www.alfredapp.com/">alfred</a> extension to easily move and resize application windows. 

</p>
<p><img src="ui/layouts.jpg" alt="Example Layout">

</p>
<h2>Alfred</h2>
<p>You can download the alfred extension <a href="https://github.com/jgallen23/layouts/raw/master/dist/Layouts.alfredextension">here</a>.

</p>
<h3>Command</h3>
<ul>
<li>resize [f, t, b, l, r, tl, tr, bl, br, c, m]</li>
</ul>
<h3>Layouts</h3>
<ul>
<li>f: full screen</li>
<li>c: center window </li>
<li>t: top half of screen</li>
<li>b: bottom half of screen</li>
<li>l: left half of screen</li>
<li>r: right half of screen</li>
<li>tl: top left quarter of screen </li>
<li>tr: top right quarter of screen </li>
<li>bl: bottom left quarter of screen </li>
<li>br: bottom right quarter of screen </li>
<li>m: move to next monitor</li>
</ul>
<h3>Hotkey support</h3>
<p>Thanks to Alfred&#39;s new hotkey feature, you don&#39;t even have to trigger alfred to move your windows around.

</p>
<p><img src="ui/layouts-hotkeys.jpg" alt="Hotkeys">

</p>
<h2>Applescript</h2>
<p>Here&#39;s the applescript that is powering the extension, which you can use by itself:

</p>
<pre><code>-- v0.0.3

on get_screens()
    set f to (path to preferences from local domain as Unicode text) &amp; &quot;com.apple.windowserver.plist&quot;

    tell application &quot;System Events&quot;
        set v to value of property list item 1 of property list item &quot;DisplaySets&quot; of property list file f
        set {scrRes1, scrRes2} to {{|OriginX|, |OriginY|, |Width|, |Height|} of beginning of v, missing value}
        if ((count v) &gt; 1) then set scrRes2 to {|OriginX|, |OriginY|, |Width|, |Height|} of item 2 of v -- Second screen.
    end tell
    return {scrRes1, scrRes2}
end get_screens

on get_screen_bounds(app_bounds)
    set screens to my get_screens()
    set app_x to item 1 of app_bounds
    set app_y to item 2 of app_bounds
    set screen1 to item 1 of screens
    if app_x ≥ item 1 of screen1 and app_y ≥ item 2 of screen1 then
        return screen1
    else
        return item 2 of screens
    end if
end get_screen_bounds

on get_next_screen(screen_bounds)
    set screens to my get_screens()
    set screen1 to item 1 of screens

    if item 1 of screen_bounds = item 1 of screen1 and item 2 of screen_bounds = item 2 of screen1 then
        return item 2 of screens
    else
        return screen1
    end if

end get_next_screen

on resize(loc)
    set perc to 0.9

    set front_app to (path to frontmost application as Unicode text)
    tell application front_app
        set app_bounds to bounds of window 1
        set screen to my get_screen_bounds(app_bounds)
        set sx to item 1 of screen
        set sy to item 2 of screen
        set sw to item 3 of screen
        set sh to item 4 of screen
        activate
        if loc contains &quot;tr&quot; then
            set x to sx + sw / 2
            set y to sy
            set w to sw + sx
            set h to sy + sh / 2
        else if loc contains &quot;tl&quot; then
            set x to sx
            set y to sy
            set w to sx + sw / 2
            set h to sy + sh / 2
        else if loc contains &quot;bl&quot; then
            set x to sx
            set y to sy + sh / 2
            set w to sx + sw / 2
            set h to sy + sh
        else if loc contains &quot;br&quot; then
            set x to sx + sw / 2
            set y to sy + sh / 2
            set w to sx + sw
            set h to sy + sh
        else if loc contains &quot;t&quot; then
            set x to sx
            set y to sy
            set w to sx + sw
            set h to sy + sh / 2
        else if loc contains &quot;r&quot; then
            set x to sx + sw / 2
            set y to sy
            set w to sx + sw
            set h to sy + sh
        else if loc contains &quot;l&quot; then
            set x to sx
            set y to sy
            set w to sx + sw / 2
            set h to sy + sh
        else if loc contains &quot;b&quot; then
            set x to sx
            set y to sy + sh / 2
            set w to sx + sw
            set h to sy + sh
        else if loc contains &quot;c&quot; then
            set x to sx + sw * (1 - perc)
            set y to sy + sh * (1 - perc)
            set w to sx + sw * perc
            set h to sy + sh * perc
        else if loc contains &quot;f&quot; then
            set x to sx
            set y to sy
            set w to sx + sw
            set h to sy + sh
        else if loc contains &quot;m&quot; then
            set next_screen to my get_next_screen(screen)
            set app_width to (item 3 of app_bounds) - (item 1 of app_bounds)
            set app_height to (item 4 of app_bounds) - (item 2 of app_bounds)
            set nsx to item 1 of next_screen
            set nsy to item 2 of next_screen
            set x to nsx
            set y to nsy
            set w to nsx + app_width
            set h to nsy + app_height
        end if
        set bounds of window 1 to {x, y, w, h}
    end tell
end resize

on alfred_script(loc)
    resize(loc)
end alfred_script

--uncomment these lines to use for command line
--on run argv
--    resize(item 1 of argv)
--end run

--this can be changed to f,t,b,l,r,tl,tr,bl,br or c
--resize(&quot;m&quot;)</code></pre>
<h3>Command Line</h3>
<p>The script will work via the command line too! <em>Make sure to uncomment the on run argv lines if you want to use it this way</em>

</p>
<pre><code>osascript layouts.applescript f</code></pre>
<p>Go <a href="https://raw.github.com/jgallen23/layouts/master/layouts.applescript">here</a> to download the script

</p>
<h2>History</h2>
<h4>0.0.3 (03/18/2012)</h4>
<ul>
<li>multi monitor support</li>
</ul>
<h4>0.0.2</h4>
<ul>
<li>bug fixes</li>
</ul>
<h4>0.0.1</h4>
<ul>
<li>initial release</li>
</ul>
<h2>Future</h2>
<ul>
<li>automatic update support</li>
<li>custom sizes</li>
</ul>
<h2>Contributors</h2>
<ul>
<li>Greg Allen (<a href="http://twitter.com/jgaui">@jgaui</a>) <a href="http://jga.me">jga.me</a></li>
</ul>
</div><script src="dist/common-51b215da.js"></script><script>$('#toc').toc();
</script></body></html>